import sokol_nature.sokol.sokol_gfx as sg
import sokol_nature.glfw.glfw_glue as gg
import sokol_nature.glfw.glfw as gf
import sokol_nature.utils as ut
import syscall
import fs

var pass_action = sg.pass_action_t {}
var bind = sg.bindings_t {}
var pip = sg.pipeline_t {}

fn init()! {
    gg.desc_t gg_desc = gg.desc_t {
        title: 'triangle',
        width: 640,
        height: 480,
        no_depth_buffer: true,
    }
    gg.init(&gg_desc)
    var sg_desc = sg.desc_t{}
    sg_desc.environment = gg.environment()
    sg.setup(&sg_desc)

    [f32;21] vertices = [
        // positions        // colors
        0.0, 0.5, 0.5,      1.0, 0.0, 0.0, 1.0,
        0.5,-0.5, 0.5,      0.0, 1.0, 0.0, 1.0,
       -0.5,-0.5, 0.5,      0.0, 0.0, 1.0, 1.0,
    ]
    //var buffer_desc = sg.buffer_desc_t { data: ut.range2(vertices, @sizeof(f32) * 21) }
    var buffer_desc = sg.buffer_desc_t { data: ut.range(vertices) }

    var vbuf = sg.make_buffer(&buffer_desc)
    bind.vertex_buffers[0] = vbuf

    var f_vert = fs.open('triangle.vert', syscall.O_RDONLY, 0)
    var f_frag = fs.open('triangle.frag', syscall.O_RDONLY, 0)
    var vertex_glsl = vec_new<u8>(0, 256)
    var fragment_glsl = vec_new<u8>(0, 256)
    f_vert.read(vertex_glsl)
    f_frag.read(fragment_glsl)

    var shader_desc = sg.shader_desc_t {}
    shader_desc.vertex_func.source = vertex_glsl.ref()
    shader_desc.fragment_func.source = fragment_glsl.ref()
    var shd = sg.make_shader(&shader_desc)

    var pipeline_desc = sg.pipeline_desc_t {}
    pipeline_desc.shader = shd
    pipeline_desc.layout.attrs[0].format = sg.VERTEXFORMAT_FLOAT3
    pipeline_desc.layout.attrs[1].format = sg.VERTEXFORMAT_FLOAT4
    pip = sg.make_pipeline(&pipeline_desc)
}

fn frame() {
    var pass = sg.pass_t {swapchain: gg.swapchain()}
    sg.begin_pass(&pass)
    sg.apply_pipeline(pip)
    sg.apply_bindings(&bind)
    sg.draw(0, 3, 1)
    sg.end_pass()
    sg.commit()
    gf.swap_buffers(gg.window())
    gf.poll_events()
}

fn cleanup() {
    sg.shutdown()
    gf.terminate()
}

fn main() {
    pass_action.colors[0].load_action = sg.LOADACTION_CLEAR
    pass_action.colors[0].clear_value = sg.color_t { r: 0.2, g: 0.0, b: 1.0, a: 1.0 }
    init()
    for !(gf.window_should_close(gg.window()) != 0)  {
        frame()
    }
    cleanup()
}

